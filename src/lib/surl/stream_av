


        lda     ser_a_avail     ; 3
        beq     check_serial    ; 5 / 6
        bmi     set_offset      ; 7 / 8

set_data:
        lda     #CMP            ; 9
        sta     set_offset      ; 13
        lda     ser_a_data      ; 16
        sta     (page_ptr),y    ; 21
        stz     ser_a_avail     ; 24
        WASTE_14                ; 38
        bra     done            ; 41

; -----------------------------------

check_serial:
        lda     (acia_status)   ; 11        - ACIA STATUS
        and     #$08            ; 13
        beq     :+              ; 15 / 16
        sta     ser_a_avail     ; 18
        lda     (acia_data)     ; 22       - ACIA DATA
        sta     ser_a_data      ; 25
        WASTE_13                ; 38
        bra     done            ; 41

:       WASTE_22                ; 38
        bra     done            ; 41

; -----------------------------------

set_offset:
        cmp     set_base        ; 12 if CMP / 11 if JMP, patched
        lda     ser_a_data      ; 15
        and     #%01111111      ; 17 - Get rid of sign
        tay                     ; 19 - set offset
        lda     #JMP            ; 21
        sta     set_offset      ; 25 - Set base if another neg number arrives
        stz     ser_a_avail     ; 28
        WASTE_10                ; 38
        bra     done            ; 41

; -----------------------------------

set_base:
        and     #%01111111      ; 13 - Get rid of the sign
        asl     a               ; 15 - Shift for array index
        tay                     ; 17
page_ptr_a:
        ; Update where to store pixels. Both these pointers are patched
        ; when switching page.
        lda     (page1_addr_ptr),y ; 22
        sta     page_ptr           ; 25
        iny                        ; 27
page_ptr_b:
        lda     (page1_addr_ptr),y ; 32
        sta     page_ptr+1         ; 35
        stz     ser_a_avail        ; 38
        bra     done               ; 41

done:
