1 LOMEM: 16384 :REM Protect hgr1
2 HOME
3 TEXT

8 INPUT "Length: ";L
12 TP = 1 : LFT = 2 : RGT = 3 : BTM = 4

15 DIM HIGHEST%(4, L)
16 DIM TREE%(L,L)

20 DEBUG = 0

30 FOR R = 1 TO L
31 FOR C = 1 TO L + 1
32 GET P$
33 IF C = L + 1 AND P$ <> CHR$(13) THEN PRINT "ERROR":STOP
34 IF C < L + 1 THEN TREE%(R, C) = ASC(P$) - ASC("0")
35 NEXT C
36 PRINT "done line ";R
38 NEXT R

39 C = 0 : R = 0 : I = 1

50 FOR SIDE = TP TO BTM
55 PRINT "Init highest side ";SIDE
60 FOR I = 1 TO L
70 HIGHEST%(SIDE, I) = -1
80 NEXT I
90 NEXT SIDE

100 PRINT "Ready!"
101 NVISIBLE=0

105 HOME
120 HGR
121 AW%=280 : AH%=160
122 TX% = (AW% - L) /2 : TY% = (AH% - L) /2
123 HCOLOR = 3

500 FOR R = 1 TO L
504 PRINT "Top/left pass on line ";R
505 FOR C = 1 TO L

509 VISIBLE = 0

540 P% = TREE%(R,C)
550 IF P% > HIGHEST%(TP,C) THEN HIGHEST%(TP,C) = P% : VISIBLE = 1

580 IF P% > HIGHEST%(LFT,R) THEN HIGHEST%(LFT,R) = P% : VISIBLE = 1

581 IF DEBUG = 1 THEN PRINT "[";R;",";C;"]: ";P%;" V ";VISIBLE;" (HT ";HIGHEST%(TP,C);", HL ";HIGHEST%(LFT,R);")"

582 IF VISIBLE = 1 THEN HPLOT TX% + C, TY% + R : TREE%(R,C) = P% + 10 : NVISIBLE = NVISIBLE + 1
620 NEXT C
630 NEXT R

2100 FOR R = L TO 1 STEP -1
2110 PRINT "Bottom/right pass on line ";R
2120 FOR C = L TO 1 STEP -1

2122 VISIBLE = 0
2123 WASVIS = 0
2200 P% = TREE%(R,C)
2210 IF P% > 9 THEN VISIBLE=1
2215 IF VISIBLE = 1 THEN P% = P% - 10 : WASVIS = 1

2550 IF P% > HIGHEST%(BTM,C) THEN HIGHEST%(BTM,C) = P%: VISIBLE=1

2580 IF P% > HIGHEST%(RGT,R) THEN HIGHEST%(RGT,R) = P%: VISIBLE=1

2600 IF VISIBLE = 1 THEN HPLOT TX% + C, TY% + R
2610 IF VISIBLE = 1 AND WASVIS = 0 THEN NVISIBLE = NVISIBLE + 1

2700 NEXT C
2710 NEXT R

3400 GET C$
3410 TEXT

3500 PRINT "Number of visible trees: ";NVISIBLE
