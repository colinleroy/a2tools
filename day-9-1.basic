2 HOME
3 TEXT

10 DEBUG = 0
15 INPUT "Read from: ";INFILE$

50 DIM X%(2) : DIM Y%(2) : H = 1 : T = 2

70 REM First simulate to figure how many moves
71 SIM = 1

80 X%(H) = 1 : Y%(H) = 1 : X%(T) = 1 : Y%(T) = 1

81 PRINT CHR$(4);"OPEN ";INFILE$
82 PRINT CHR$(4);"READ ";INFILE$

83 ONERR GOTO 191

89 IF SIM = 0 THEN DIM MOV%(NMOV%,2) : MOV%(1,1) = X%(T) : MOV%(1,2) = Y%(T)

90 HOME
91 I = 1 : NMOV% = 1 : NVIS% = 0
100 INPUT MV$

110 DLTA% = VAL(MID$(MV$, 3, 5))
120 GOSUB 9000

125 IF SIM = 1 THEN NMOV% = NMOV% + DLTA% : GOTO 185

130 FOR N = 1 TO DLTA%

150 X%(H) = X%(H) + DX% : Y%(H) = Y%(H) + DY%

160 GOSUB 8000

170 X%(T) = X%(T) + WX% : Y%(T) = Y%(T) + WY%

171 NMOV% = NMOV% + 1

173 GOSUB 6000

180 NEXT N
185 PRINT "Did line ";I;" visited: ";NVIS%
186 I = I + 1
190 GOTO 100

191 POKE 216,0 : PRINT CHR$(4);"CLOSE ";INFILE$
193 IF SIM = 1 THEN PRINT "Sim done. Number of moves ";NMOV% : SIM = 0 : GET C$: GOTO 80

260 PRINT "Tail visited ";NVIS%;" spots"

270 GOTO 10000

6000 REM Count spot
6010 IF NVIS% = 0 THEN NVIS% = NVIS% + 1 : RETURN
6020 FOR J = 1 TO NVIS%
6030 IF MOV%(J,1) = X%(T) AND MOV%(J,2) = Y%(T) THEN RETURN
6040 NEXT J
6050 NVIS% = NVIS% + 1 : MOV%(NVIS%,1) = X%(T) : MOV%(NVIS%,2) = Y%(T)
6060 RETURN

8000 REM Calculate tail move
8010 WX% = 0 : WY% = 0 : XDF% = X%(H) - X%(T) : YDF% = Y%(H) - Y%(T)
8020 IF ABS(XDF%) < 2 AND ABS(YDF%) < 2 THEN RETURN
8030 IF ABS(XDF%) > 0 THEN WX% = XDF%/ABS(XDF%)
8040 IF ABS(YDF%) > 0 THEN WY% = YDF%/ABS(YDF%)
8050 RETURN


9000 REM Get directions (Up/down inverted for > 0)
9010 DX% = 0: DY% = 0: A% = ASC(MV$)
9020 IF A% = 68 THEN DY% = -1 : RETURN
9030 IF A% = 85 THEN DY% = 1 : RETURN
9040 IF A% = 76 THEN DX% = -1 : RETURN
9050 IF A% = 82 THEN DX% = 1 : RETURN
9060 PRINT "What are we doing here?";I;",";MV$ : STOP

10000 PRINT CHR$(4);"CLOSE ";INFILE$ : GET C$ : PRINT "Done"
