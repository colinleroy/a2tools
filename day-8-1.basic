1 HOME

8 INPUT "Length: ";L
12 TP = 1 : LFT = 2 : RGT = 3 : BTM = 4

15 DIM HIGHEST%(4, L)
16 DIM TREE%(L,L)

20 DEBUG = 0

30 FOR R = 1 TO L
31 FOR C = 1 TO L + 1
32 GET P$
33 IF C = L + 1 AND P$ <> CHR$(13) THEN PRINT "ERROR":STOP
34 IF C < L + 1 THEN TREE%(R, C) = ASC(P$) - ASC("0")
35 NEXT C
36 PRINT "done line ";R
38 NEXT R

39 C = 0 : R = 0 : I = 1

50 FOR SIDE = TP TO BTM
55 PRINT "Init highest side ";SIDE
60 FOR I = 1 TO L
70 HIGHEST%(SIDE, I) = -1
80 NEXT I
90 NEXT SIDE

100 PRINT "Ready!"

500 FOR R = 1 TO L
504 PRINT "Top/left pass on line ";R
505 FOR C = 1 TO L

509 VISIBLE = 0

540 P% = TREE%(R,C)
550 IF P% > HIGHEST%(TP,C) THEN HIGHEST%(TP,C) = P% : VISIBLE = 1

580 IF P% > HIGHEST%(LFT,R) THEN HIGHEST%(LFT,R) = P% : VISIBLE = 1

581 IF DEBUG = 1 THEN PRINT "[";R;",";C;"]: ";P%;" V ";VISIBLE;" (HT ";HIGHEST%(TP,C);", HL ";HIGHEST%(LFT,R);")"

585 IF VISIBLE = 1 THEN  TREE%(R,C) = P% + 10
620 NEXT C
630 NEXT R

2000 PRINT "Doing bottom/right on ";L;" rows, "; L;" cols"

2100 FOR R = L TO 1 STEP -1
2110 PRINT "Bottom/right pass on line ";R
2120 FOR C = L TO 1 STEP -1

2122 VISIBLE=0

2200 P% = TREE%(R,C)
2210 IF P% > 9 THEN VISIBLE=1
2215 IF VISIBLE = 1 THEN P% = P% - 10

2550 IF P% > HIGHEST%(BTM,C) THEN HIGHEST%(BTM,C) = P%: VISIBLE=1

2580 IF P% > HIGHEST%(RGT,R) THEN HIGHEST%(RGT,R) = P%: VISIBLE=1

2590 IF DEBUG = 1 THEN PRINT "[";R;",";C;"]: ";P%;" V ";VISIBLE;" (HB ";HIGHEST%(BTM,C);", HR ";HIGHEST%(RGT,R);")"

2610 IF VISIBLE = 1 THEN TREE%(R,C) = P% + 10

2700 NEXT C
2710 NEXT R

3000 NVISIBLE = 0
3010 FOR R = 1 TO L
3110 FOR C = 1 TO L

3205 P% = TREE%(R,C)
3210 VISIBLE = 0
3230 IF P% > 9 THEN VISIBLE = 1
3235 IF VISIBLE = 1 THEN NVISIBLE = NVISIBLE + 1

3240 IF DEBUG = 1 AND VISIBLE = 1 THEN PRINT "X";
3250 IF DEBUG = 1 AND VISIBLE = 0 THEN PRINT " ";

3300 NEXT C
3310 IF DEBUG = 1 THEN PRINT
3320 NEXT R

3500 PRINT "Number of visible trees: ";NVISIBLE
